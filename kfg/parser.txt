Start -> Sections

%Ugly Patch: Collect any garbage keyword at EOF - WDL allowed it
%Sections -> Section Sections | EPSILON
Sections -> Section Sections | Keyword | EPSILON
Section -> ObjectDef | FunctionDef | AssetDef | Define | Include | GlobalDef | Preprocessor | } | ;
%Superfluous closing curly braces and semicolons may be put - capture these as dummy section

Preprocessor -> IFDEF DefIdentifier ; PreprocessorSection ; | IFNDEF DefIdentifier ; PreprocessorSection ;
PreprocessorSection -> Sections ENDIF | Sections IFELSE ; Sections ENDIF
PreprocInst -> IFDEF DefIdentifier PreprocInstSection | IFNDEF DefIdentifier PreprocInstSection
PreprocInstSection -> Instructions ENDIF | Instructions IFELSE Instructions ENDIF
PreprocProp -> IFDEF DefIdentifier ; PreprocPropSection ; | IFNDEF DefIdentifier ; PreprocPropSection ;
PreprocPropSection -> PropertyDefs ENDIF | PropertyDefs IFELSE ; PropertyDefs ENDIF

Define -> DEFINE DefIdentifier , DefParameter ; | DEFINE DefIdentifier ; | UNDEF DefIdentifier ;
%Allow ambiguity between predefined skills and defines. WDL allowed it.
%TODO: is this suffcient or more general approach needed? Just allow "BasicKeyword"?
DefIdentifier -> Identifier | Skill
%TODO
DefParameter -> Number | KeywordPar | File | String | List
Include -> INCLUDE File ;

GlobalDef -> Global Sep GlobalParameter ; | Event Sep ObjectIdList ;
Global -> global | ambig_global_property | ambig_global_synonym_property | Skill
Event -> event | ambig_event_property
ObjectIdList -> ObjectId Sep ObjectIdList | ObjectId Sep
GlobalParameter -> Number | File | String | Identifier

AssetDef -> AssetRef AssetId Sep File Sep AssetParams ;
AssetParams -> AssetParam | AssetParam , AssetParams | EPSILON 
%Assets must also allow redefinitions (= identifier) TODO: is "Identifier" sufficient?
AssetParam -> Integer | Identifier
AssetRef -> asset

ObjectDef -> Object ObjectId ; | Object ObjectId Sep { PropertyDefs } | Object ObjectId Sep String ;
ObjectRef -> object
%TODO is this special case still required?
Object -> ObjectRef | ambig_object_flag 
PropertyDefs -> PropertyDef ; PropertyDefs | PreprocProp PropertyDefs | EPSILON
PropertyDef -> Property Sep PropertyValueList
PropertyValueList -> PropertyValue Sep PropertyValueList | PropertyValue Sep 
PropertyValue -> Number | File | String | KeywordPar

FunctionDef -> Function FunctionId Sep { Instructions }
Function -> function
%Compatibility patch - Goto markers may also be terminated by ; instead of : (use identifier instead of GotoMarker for patch; causes reduce/reduce conflicts otherwise)
Instructions -> Instruction Instructions | GotoMarker : Instructions | identifier ; Instructions | Flow Instructions | PreprocInst Instructions | { Instructions } Instructions | EPSILON
%Compatibility patch - DEFINEs can be placed inside functions - A3 allows bogus instructions (capture as Identifier)
%Instruction -> Command ; | Command Sep ParameterList ; | Rule ; | ;
Instruction -> Command ; | Command Sep ParameterList ; | Rule ; | Define | Identifier Sep ParameterList ; | ;

ParameterList -> Parameter Sep ParameterList | Parameter Sep
Parameter -> Number | File | String | KeywordPar | List

%Compatibility patch - WDL parser allows leaving/adding commas
Sep -> , | EPSILON

Expression -> LogicOr
LogicOr -> LogicOr '||' LogicAnd | LogicAnd
LogicAnd -> LogicAnd && BitOr | BitOr
BitOr -> BitOr '|' BitXor | BitXor
BitXor -> BitXor ^ BitAnd | BitAnd
BitAnd -> BitAnd & Equality | Equality
Equality -> Equality OpEq Comparison | Comparison
Comparison -> Comparison OpCmp Term | Term
Term -> Term OpTerm Factor | Factor 
Factor -> Factor OpFac Unary | Unary 
Unary -> OpUn Unary | Primary
Primary -> UnNumber | Keyword | ( Expression ) | Math ( Expression ) | List

OpEq -> == | !=
OpCmp -> <= | >= | < | > 
OpTerm -> + | -
OpFac -> / | * | %
OpUn -> - | + | !

%WDL parser allowed RULEs without assignment
Rule -> RULE Keyword OpAssign Expression | RULE Expression | Keyword OpAssign Expression
OpAssign -> = | += | -= | *= | /=
Math -> math | ambig_math_command | ambig_math_skill_property

%Compatibility patch - WDL parser accepts isolated ELSE ... Needs to be taken care of by code generator
%Flow -> IF Expression { Instructions } | IF Expression { Instructions } ELSE { Instructions } | WHILE Expression { Instructions }
Flow -> IF Expression { Instructions } | ELSE { Instructions } | WHILE Expression { Instructions }

Keyword -> BasicKeyword | BasicKeyword . Property | BasicKeyword . Flag
KeywordPar -> OpUn Keyword | Keyword
Flag -> flag | ambig_synonym_flag | ambig_object_flag | ambig_command_flag | ambig_skill_flag
%Properties must also allow redefinitions (= identifier) TODO: is "Identifier" sufficient?
%Property -> property | AssetRef | ObjectRef | ambig_event_property | ambig_skill_property | ambig_global_synonym_property | ambig_global_property | ambig_math_skill_property | ambig_command_property
Property -> property | AssetRef | ObjectRef | ambig_event_property | ambig_skill_property | ambig_global_synonym_property | ambig_global_property | ambig_math_skill_property | ambig_command_property | Identifier
Command -> command | ambig_command_flag | ambig_command_property | ambig_math_command | ELSE
Number -> Integer | Fixed
UnNumber -> integer | fixed
Integer -> OpUn integer | integer
Fixed -> OpUn fixed | fixed
Identifier -> identifier
List -> list

Skill -> skill | ambig_skill_property | ambig_math_skill_property

EngineKeywords -> event | global | asset | object | function | math | flag | property | command | ambig_global_property | ambig_event_property | ambig_object_flag | ambig_math_command | ambig_command_flag | ambig_command_property
EngineObjects -> skill | synonym | ambig_math_skill_property | ambig_synonym_flag | ambig_skill_property | ambig_global_synonym_property | ambig_skill_flag
BasicKeyword -> EngineKeywords | EngineObjects | Identifier | NULL
ObjectId -> BasicKeyword
FunctionId -> BasicKeyword
%Note: Asset identifiers may start with numbers in WDL. Needs to be captured with regex and taken care of by code generator
AssetId -> BasicKeyword | integer
GotoMarker -> BasicKeyword

%Complex Regex terminals must be added at end of token list
File -> file
String -> string

%Ambiguous keyword lists - used in case some keywords are used multiple times in different contexts
%- ambig_event_property
%- ambig_skill_property
%- ambig_global_property
%- ambig_global_synonym_property
%- ambig_synonym_flag
%- ambig_object_flag
%- ambig_math_command
%- ambig_math_skill_property
%- ambig_command_property
%- ambig_skill_flag

%Regex patches - Odd behavior captured with regex and needs to be taken care of by code generator
%- Combined assign operators += etc. may have spaces inbetween. 
%- Comma separator may be put multiple times and at end of instruction right before ;. 
%- File name may have leading and traling spaces e.g. <file.wdl >. TODO: cleaner filename detection
%- skills may be defined like global definitions
%- SET instruction allows := instead of , as assignment operator
