Start -> Sections


Sections -> Section Sections | EPSILON
Section -> SimpleItem | ComplexItem | Asset | Define | Include | Predefined | Preprocessor

Preprocessor -> IFDEF Identifier ; PreprocessorSection | IFNDEF Identifier ; PreprocessorSection 
PreprocessorSection -> Sections ENDIF; | Sections IFELSE; Sections ENDIF;
PreprocInst -> IFDEF Identifier ; PreprocInstSection | IFNDEF Identifier ; PreprocInstSection 
PreprocInstSection -> Instructions ENDIF; | Instructions IFELSE; Instructions ENDIF;

Define -> DEFINE Identifier , Parameter ; | DEFINE Identifier ; | UNDEF Identifier ;
Include -> INCLUDE File ;

%TODO name this properly
Predefined -> Identifier PredefinedParameter ;
PredefinedParameter -> Number | File | String | Keyword

Asset -> BasicAsset AssetId Sep File ; | BitmapAsset AssetId Sep File ; | BitmapAsset AssetId Sep File Sep Integer , Integer , Integer , Integer ; | FontAsset
BasicAsset -> MODEL | SOUND | MUSIC | FLIC
BitmapAsset -> BMAP | OVLY
FontAsset -> FONT AssetId Sep File Sep Integer , Integer ; | FONT AssetId Sep File Sep Integer , Integer , Integer , Integer , Integer , Integer ;
AssetRef -> BasicAsset | BitmapAsset | BMAPS | OVLYS | FONT

SimpleItem -> Identifier ObjectId Sep ParameterList ; 
%TODO: clashes with Predefined Rule - introduce Object (similar to Asset)
%| Identifier ObjectId ;

%TODO: support RULES{} which is a group of RULE statements (a = b;)
ComplexItem -> Identifier ObjectId Sep { Instructions }
Instructions -> Instruction ; Instructions | Identifier : Instructions | Flow Instructions | PreprocInst Instructions | | { Instructions } Instructions | EPSILON
%TODO: This is a hotfix only. Needs to be resolved once Object and Object.Properties are in place
Instructions -> AssetRef : Instructions

Instruction -> Identifier | Identifier Sep ParameterList | Rule | AssetRef Sep AssetIdList

ParameterList -> Parameter Sep ParameterList | Parameter Sep
Parameter -> Number | Keyword | File | String
AssetIdList -> AssetIdNull Sep AssetIdList | AssetIdNull Sep
AssetIdNull -> AssetId | NULL

%Compatibility patches - WDL parser allowed syntax violating the specification
%WDL parser allows leaving/adding commas
Sep -> , | EPSILON

Expression -> LogicOr
LogicOr -> LogicOr '||' LogicAnd | LogicAnd
LogicAnd -> LogicAnd && BitOr | BitOr
BitOr -> BitOr '|' BitXor | BitXor
BitXor -> BitXor ^ BitAnd | BitAnd
BitAnd -> BitAnd & Equality | Equality
Equality -> Equality OpEq Comparison | Comparison
Comparison -> Comparison OpCmp Term | Term
Term -> Term OpTerm Factor | Factor 
Factor -> Factor OpFac Unary | Unary 
Unary -> OpUn Unary | Primary
Primary -> UnNumber | Keyword | ( Expression ) | Math ( Expression )

OpEq -> == | !=
OpCmp -> <= | >= | < | > 
OpTerm -> + | -
OpFac -> / | * | %
OpUn -> ! | - | +

%WDL parser allowed RULEs without assignment
Rule -> RULE Keyword OpAssign Expression | RULE Expression
OpAssign -> = | += | -= | *= | /=
Math -> SIN | COS | TAN | ASIN | ACOS | SQRT | SIGN | ABS | INT | EXP | LOG | LOG10 | LOG2 | RANDOM

%WDL parser accepts isolated ELSE ... Needs to be taken care of by code generator
%Flow -> IF Expression{ Instructions } | IF Expression { Instructions } ELSE { Instructions } | WHILE Expression { Instructions }
Flow -> IF Expression { Instructions } | ELSE { Instructions } | WHILE Expression { Instructions }

%this needs cleanup. ObjectId needs to be allowed here, but clashes currently with Identifier, therefore AssetRef is added only
%Split ACTION from all other Objects, and introduce Property Type for Objects for avoiding ambiguity
%Keyword -> Identifier | Identifier . Identifier | NULL
Keyword -> ObjectId | ObjectId . Identifier | NULL | ObjectId . AssetRef
Number -> Integer | Fixed
UnNumber -> integer | fixed
Integer -> OpUn integer | integer
Fixed -> OpUn fixed | fixed

%Math function identifiers are ambiguous in original WDL syntax. can be object parameter/action instruction as well as math function
Identifier -> identifier | Math
%Note: Identifiers may start with numbers in WDL. Needs to be captured with regex and taken care of by code generator
%Note: Identifiers support dot-separated slot index, e.g. LAYERS.1. Needs to be captured with regex and taken care of by code generator
AssetId -> identifier | integer
%WDL allows usage of reserved keywords as object identifiers
ObjectId -> identifier | AssetRef | Math

%Complex Regex terminals must be added at end of token list
File -> file
String -> string

%Regex patches - Odd behavior captured with regex and needs to be taken care of by code generator
%Combined assign operators += etc. may have spaces inbetween. 
%Command terminator ; may be put multiple times without error. 
%Comma separator may be put at end of instruction right before ;. 
%A false ; may be put right after closing curly brace.
%File name may have leading and traling spaces e.g. <file.wdl >. TODO: cleaner filename detection