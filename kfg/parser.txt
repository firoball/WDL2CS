Start -> Sections

Sections -> Section Sections | EPSILON
Section -> ObjectDef | FunctionDef | AssetDef | Define | Include | GlobalDef | Preprocessor

Preprocessor -> IFDEF Identifier ; PreprocessorSection ; | IFNDEF Identifier ; PreprocessorSection ;
PreprocessorSection -> Sections ENDIF | Sections IFELSE ; Sections ENDIF
PreprocInst -> IFDEF Identifier ; PreprocInstSection ; | IFNDEF Identifier ; PreprocInstSection ;
PreprocInstSection -> Instructions ENDIF | Instructions IFELSE ; Instructions ENDIF
PreprocProp -> IFDEF Identifier ; PreprocPropSection ; | IFNDEF Identifier ; PreprocPropSection ;
PreprocPropSection -> PropertyDefs ENDIF | PropertyDefs IFELSE ; PropertyDefs ENDIF

Define -> DEFINE Identifier , Parameter ; | DEFINE Identifier ; | UNDEF Identifier ;
Include -> INCLUDE File ;

GlobalDef -> Global GlobalParameter ; | Event ObjectIdList ;
Global -> global | ambig_global_property | ambig_global_synonym_property
Event -> event | ambig_event_property
ObjectIdList -> ObjectId Sep ObjectIdList | ObjectId Sep
GlobalParameter -> Number | File | String | Identifier

AssetDef -> AssetRef AssetId Sep File Sep AssetParams ;
AssetParams -> Integer | Integer , AssetParams | EPSILON 
AssetRef -> asset

ObjectDef -> Object ObjectId ; | Object ObjectId Sep { PropertyDefs } | Object ObjectId Sep String ;
ObjectRef -> object
Object -> ObjectRef | ambig_object_flag 
PropertyDefs -> PropertyDef ; PropertyDefs | PreprocProp PropertyDefs | EPSILON
PropertyDef -> Property Sep PropertyValueList
PropertyValueList -> PropertyValue Sep PropertyValueList | PropertyValue Sep 
PropertyValue -> Number | ObjectId | ObjectId . Property | Object | Function | File | String | NULL

FunctionDef -> Function FunctionId Sep { Instructions }
Function -> function
%TODO: Goto markers may also be terminated by ; instead of :
Instructions -> Instruction ; Instructions | GotoMarker : Instructions | Flow Instructions | PreprocInst Instructions | | { Instructions } Instructions | EPSILON
Instruction -> Command | Command Sep ParameterList | Rule

ParameterList -> Parameter Sep ParameterList | Parameter Sep
Parameter -> Number | Keyword | File | String | List

%Compatibility patches - WDL parser allowed syntax violating the specification
%WDL parser allows leaving/adding commas
Sep -> , | EPSILON

Expression -> LogicOr
LogicOr -> LogicOr '||' LogicAnd | LogicAnd
LogicAnd -> LogicAnd && BitOr | BitOr
BitOr -> BitOr '|' BitXor | BitXor
BitXor -> BitXor ^ BitAnd | BitAnd
BitAnd -> BitAnd & Equality | Equality
Equality -> Equality OpEq Comparison | Comparison
Comparison -> Comparison OpCmp Term | Term
Term -> Term OpTerm Factor | Factor 
Factor -> Factor OpFac Unary | Unary 
Unary -> OpUn Unary | Primary
Primary -> UnNumber | Keyword | ( Expression ) | Math ( Expression )

OpEq -> == | !=
OpCmp -> <= | >= | < | > 
OpTerm -> + | -
OpFac -> / | * | %
OpUn -> - | + | !

%WDL parser allowed RULEs without assignment
Rule -> RULE Keyword OpAssign Expression | RULE Expression | Keyword OpAssign Expression
OpAssign -> = | += | -= | *= | /=
Math -> math | ambig_math_command | ambig_math_skill_property

%WDL parser accepts isolated ELSE ... Needs to be taken care of by code generator
%Flow -> IF Expression{ Instructions } | IF Expression { Instructions } ELSE { Instructions } | WHILE Expression { Instructions }
Flow -> IF Expression { Instructions } | ELSE { Instructions } | WHILE Expression { Instructions }

%Due to parallel grammar pathes caused by ambiguous identifiers, some expected relations are not added here - they already exist implicitly
%This is required in order to avoid shift/reduce conflicts in generated transpiler code
Keyword -> Object | ObjectId | ObjectId . Property | ObjectId . Flag | Synonym | Synonym . Property | Synonym . Flag | Event | NULL
Flag -> flag | ambig_synonym_flag | ambig_object_flag | ambig_command_flag
%TODO: Properties must also allow redefinitions (= identifier)
Property -> property | AssetRef | ObjectRef | ambig_event_property | ambig_skill_property | ambig_global_synonym_property | ambig_global_property | ambig_math_skill_property | ambig_command_property
Command -> CommandAsId | ambig_math_command | ELSE
%commands can also be used as object identifiers. Define reduced CommandAsId subset for ObjectId rule in order to avoid shift/reduce conflicts.
CommandAsId -> command | ambig_command_flag | ambig_command_property 
Number -> Integer | Fixed
UnNumber -> integer | fixed
Integer -> OpUn integer | integer
Fixed -> OpUn fixed | fixed
Identifier -> identifier
List -> list

%Note: Asset identifiers may start with numbers in WDL. Needs to be captured with regex and taken care of by code generator
%AssetId -> identifier | integer
AssetId -> ObjectId | integer
Skill -> skill | ambig_skill_property 
%| ambig_math_skill_property - implicitly via Math (avoid shift/reduce conflicts)

Synonym -> synonym | ambig_synonym_flag | ambig_global_synonym_property
%WDL allows usage of reserved keywords as object identifiers (list is derived from test runs on a collection of WDL files)
%Changing this can easily cause more shift/reduce conflicts
ObjectId -> identifier | AssetRef | Math | flag | Skill | property | CommandAsId | global
FunctionId -> ObjectId | ObjectRef
GotoMarker -> ObjectId | synonym

%Complex Regex terminals must be added at end of token list
File -> file
String -> string

%Ambiguous keyword lists - used in case some keywords are used multiple times in different contexts
%ambig_event_property
%ambig_skill_property
%ambig_global_property
%ambig_global_synonym_property
%ambig_synonym_flag
%ambig_object_flag
%ambig_math_command
%ambig_math_skill_property
%ambig_command_property

%Regex patches - Odd behavior captured with regex and needs to be taken care of by code generator
%Combined assign operators += etc. may have spaces inbetween. 
%Command terminator ; may be put multiple times without error. 
%Comma separator may be put at end of instruction right before ;. 
%A false ; may be put right after closing curly brace.
%File name may have leading and traling spaces e.g. <file.wdl >. TODO: cleaner filename detection
